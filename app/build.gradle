apply plugin: 'com.android.application'

apply plugin: 'com.vanniktech.android.junit.jacoco'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion compiled_sdk_version

    defaultConfig {
        applicationId "com.kodebonek.qurannotes"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testHandleProfiling true
        testFunctionalTest true

        testOptions {
            reportDir "$rootDir/test-reports"
            resultsDir "$rootDir/test-results"
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }

    }

    buildTypes {
        debug {
            testCoverageEnabled true
            minifyEnabled false
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    sourceSets {
        test.java.srcDirs += "src/test-common/java"
        androidTest.java.srcDirs += "src/test-common/java"
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    lintOptions {
        abortOnError false
    }

}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"
    implementation "com.android.support:appcompat-v7:${support_version}"
    implementation "com.android.support:design:${support_version}"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    implementation "com.jakewharton.timber:timber:${timber_version}"

    //archi
    implementation "android.arch.lifecycle:extensions:${android_arch_version}"
    implementation "android.arch.persistence.room:runtime:${android_room_version}"

    //dagger
    implementation "com.google.dagger:dagger:${dagger_version}"
    implementation "com.google.dagger:dagger-android:${dagger_version}"
    implementation "com.google.dagger:dagger-android-support:${dagger_version}"

    //Retrofit 2
    implementation "com.squareup.retrofit2:retrofit:${retrofit_version}"
    implementation "com.squareup.retrofit2:adapter-rxjava:${retrofit_version}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofit_version}"
    implementation "com.google.code.gson:gson:${gson_version}"

    //experimental - coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${kotlin_coroutines_version}"

    implementation "com.squareup.okhttp3:logging-interceptor:3.10.0"

    //kapt
    kapt "com.google.dagger:dagger-compiler:${dagger_version}"
    kapt "com.google.dagger:dagger-android-processor:${dagger_version}"
    kapt "android.arch.persistence.room:compiler:${android_room_version}"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:${mockito_version}"
    testImplementation "org.hamcrest:hamcrest-library:${hamcrest_version}"
    testImplementation "android.arch.core:core-testing:${android_arch_version}"
    testImplementation "android.arch.persistence.room:testing:${android_room_version}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${mock_web_server_version}"
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestImplementation "org.mockito:mockito-core:${mockito_core_version}"
    androidTestImplementation "com.android.support.test:runner:${android_test_runner_version}"
    androidTestImplementation "com.android.support.test:rules:${android_test_runner_version}"
}
